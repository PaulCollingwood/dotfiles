# Prompt based on Pure by Sindre Sorhus
# https://github.com/sindresorhus/pure
# MIT License


# Change this to your own username
DEFAULT_USERNAME='matthew'

# Threshold (sec) for showing cmd exec time
CMD_MAX_EXEC_TIME=5


# For my own and others sanity
# git:
# %b => current branch
# %a => current action (rebase/merge)
# prompt:
# %F => color dict
# %f => reset color
# %~ => current path
# %* => time
# %n => username
# %m => shortname host
# %(?..) => prompt conditional - %(condition.true.false)

autoload -Uz vcs_info
zstyle ':vcs_info:*' enable git # You can add hg too if needed: `git hg`
zstyle ':vcs_info:git*' formats       ' %F{white} %b'
zstyle ':vcs_info:git*' actionformats ' %F{white} %b|%a'

# Show username if not default or if on another server
[[ $USER != $DEFAULT_USERNAME || ! -z $SSH_CONNECTION ]] && local username='%n@%m '

git_status() {
    if [ ! -z $vcs_info_msg_0_ ]
    then
        echo -n '%F{black}%K{green}'

        echo -n $vcs_info_msg_0_
        # Fastest possible way to check if repo is dirty
        # See: https://gist.github.com/sindresorhus/3898739
        git diff --quiet --ignore-submodules HEAD 2>/dev/null; [ $? -eq 1 ] && echo -n '%B%F{yellow} M %b' || echo -n ' '

        echo '%F{green}%K{black}'
    fi
}

# Displays the exec time of the last command if set threshold was exceeded
cmd_exec_time() {
    local stop=`date +%s`
    local start=${cmd_timestamp:-$stop}
    let local elapsed=$stop-$start
    [ $elapsed -gt $CMD_MAX_EXEC_TIME ] && echo ${elapsed}s
}

preexec() {
    cmd_timestamp=`date +%s`
}

precmd() {
    vcs_info
    # Add `%*` to display the time
    print -P "\n%F{black}%K{white}%~ %F{white}%K{black}`git_status` %F{236}$username%f %F{yellow}`cmd_exec_time`%f%F{black}%k"

    # Reset value since `preexec` isn't always triggered
    unset cmd_timestamp
}

# Prompt turns red if the previous command didn't exit with 0
PROMPT='%(?.%F{green}.%F{red})❯%f '
# Can be disabled:
# PROMPT='%F{magenta}❯%f '

# Change Terminal / Screen / TMux title for terminal when moving to another machine
if [ ! -z $SSH_CONNECTION ]
then
    echo -ne "\033k${USER}@${HOST}\033\\"
fi

# vim: set ai et sw=4 syntax=zsh :
