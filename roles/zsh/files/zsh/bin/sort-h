#!/usr/bin/perl
# This will order a given list of file sizes that have been provided in
# human readable format. This supports the following sizes:
# K M G T

use strict;
use warnings;

# This converts a human readable size into
# the absolute size that it represents.
sub amount {
    my ( $amount ) = @_;

    if ( $amount =~ m/([\d.]+)([KMGT]?)/ ) {
        my ( $value, $magnitude ) = ( $1, $2 );

        while ( $magnitude && $magnitude ne ' ' ) {
            $value     *= 1024;
            $magnitude =~ tr/TGMK/GMK /;
        }

        return $value;
    }
    die "Cannot process the line:\n$amount";
}

foreach ( sort { amount( $b ) <=> amount( $a ) } <> ) {
    print;
}

# vim: set ai et sw=4 syntax=perl :
