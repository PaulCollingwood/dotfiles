#!/bin/zsh
#

setopt extended_glob

COMMAND=${0:t}
DOTFILES_DIR=${0:A:h}

function help () {
    cat <<EOF
${COMMAND} [server] [server] ...

This will provision the servers listed on the command line.
This uses the small preset to do so, which is run without super user privileges.

The server can be specified with a port like so:

    example.com:2222

If the port is not specified then port 22 is used.

If you wish to alter the user and authentication this uses to connect to the
remote server then update your ~/.ssh/config file.
EOF
}

function error () {
    echo $@ >&2
}

function prepare () {
    (
        cd ${DOTFILES_DIR}
        git submodule update --init --recursive
    )
}

function provision () {
    local server

    server=$1
    (
        cd ${DOTFILES_DIR}
        ansible-playbook -i =(printf '[default]\n%s\n' "${server}") playbook-small.yml
    )
}

if [ $#@ -eq 0 ]
then
    error "No server specified"
    help
    exit 1
fi

prepare || error "Cannot prepare the dotfiles project. Make sure this is a valid git project"
for server in $@
do
    provision "${server}"
done

# vim: set ai et sw=4 syntax=zsh :
